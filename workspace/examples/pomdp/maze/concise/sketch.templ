pomdp

const int CMAX;
const double THRESHOLD;

// 3 | x x x x x
// 2 | x   x   x
// 1 | x   x   x
// 0 | x   x   x
// y ____________
//   x 0 1 2 3 4

// wall detector
formula wn = y=3;
formula we = (x=4&y=3) | y<=2;
formula ws = (x=1&y=3) | (x=3&y=3) | y=0;
formula ww = (x=0&y=3) | y<=2;

// target cell
formula goal = x=2&y=0;

// corresponding observables
observable "wn" = clk=0 & wn;
observable "we" = clk=0 & we;
observable "ws" = clk=0 & ws;
observable "ww" = clk=0 & ww;
observable "goal" = goal;


// modules

module clock
    // -1 - init, 0 - steer, 1 - drive
    clk : [-1..1] init -1;

    // random placement
    [place] clk=-1 -> (clk'=0);
    
    // steering
    [go_north] clk=0 -> (clk'=1);
    [go_east]  clk=0 -> (clk'=1);
    [go_south] clk=0 -> (clk'=1);
    [go_west]  clk=0 -> (clk'=1);

    // driving
    [north] clk=1 -> (clk'=0);
    [east]  clk=1 -> (clk'=0);
    [south] clk=1 -> (clk'=0);
    [west]  clk=1 -> (clk'=0);
endmodule

module steerer
    v : [0..4] init 0;
    [go_north] v=0 -> 0.8: (v'=1) + 0.08: (v'=2) + 0.08: (v'=4) + 0.04: (v'=3);
    [go_east]  v=0 -> 0.8: (v'=2) + 0.08: (v'=3) + 0.08: (v'=1) + 0.04: (v'=4);
    [go_south] v=0 -> 0.8: (v'=3) + 0.08: (v'=4) + 0.08: (v'=2) + 0.04: (v'=1);
    [go_west]  v=0 -> 0.8: (v'=4) + 0.08: (v'=1) + 0.08: (v'=3) + 0.04: (v'=2);

    [north] v=1 -> (v'=0);
    [east]  v=2 -> (v'=0);
    [south] v=3 -> (v'=0);
    [west]  v=4 -> (v'=0);
endmodule

module maze

	x : [-1..4] init -1;
    y : [-1..3] init -1;
	
	// initialisation
	[place] x=-1 ->
          1/13 :    (x'=0)&(y'=0)
        + 1/13 :    (x'=0)&(y'=1)
		+ 1/13 :    (x'=0)&(y'=2)
		+ 1/13 :    (x'=0)&(y'=3)
		+ 1/13 :    (x'=1)&(y'=3)
		+ 1/13 :    (x'=2)&(y'=0)
		+ 1/13 :    (x'=2)&(y'=1)
		+ 1/13 :    (x'=2)&(y'=2)
		+ 1/13 :    (x'=2)&(y'=3)
		+ 1/13 :    (x'=3)&(y'=3)
		+ 1/13 :    (x'=4)&(y'=1)
		+ 1/13 :    (x'=4)&(y'=2)
		+ 1/13 :    (x'=4)&(y'=3);

	// moving around the maze
	[north] wn -> true;
    [north] !wn -> (y'=y+1);
    
    [east] we -> true;
    [east] !we -> (x'=x+1);

    [south] ws -> true;
    [south] !ws -> (y'=y-1);

    [west] ww -> true;
    [west] !ww -> (x'=x-1);

endmodule

// rewards

rewards "steps"
	[north] true : 1;
    [east] true : 1;
    [south] true : 1;
    [west] true : 1;
endrewards

