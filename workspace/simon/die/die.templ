// Knuth's model of a fair die using only fair coins
mdp

const double p0;
const double p1=0.5;
const double p2=0.5;
const double p3=0.5;
const double p4=0.5;
const double p5=0.5;
const double p6=0.5;

const int x3;
const int x4;
const int x5;
const int x6;

module die

	// local state
	s : [0..7] init 0;
	// value of the dice
	d : [0..6] init 0;

        [] s=0 -> p0 : (s'=1) + (1-p0) : (s'=2);
        [] s=1 -> p1 : (s'=x3) + (1-p1) : (s'=x4);
        [] s=2 -> p2 : (s'=x5) + (1-p2) : (s'=x6);
        [] s=3 -> p3 : (s'=1) + (1-p3) : (s'=7) & (d'=1);
        [] s=4 -> p4 : (s'=7) & (d'=3) + (1-p4) : (s'=7) & (d'=2);
        [] s=5 -> p5 : (s'=7) & (d'=4) + (1-p5) : (s'=7) & (d'=5);
        [] s=6 -> p6 : (s'=7) & (d'=6) + (1-p6) : (s'=2);
        [] s=7 -> 1: (s'=7);
endmodule

rewards "coin_flips"
	(s<7) : 1;
endrewards

label "one" = s=7 & d=1;
label "two" = s=7 & d=2;
label "three" = s=7 & d=3;
label "four" = s=7 & d=4;
label "five" = s=7 & d=5;
label "six" = s=7 & d=6;
label "done" = s=7;
